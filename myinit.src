;  MyPicoDos init V3.0
;
;  Copyright (C) 1992-2003 Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

MYPDLEN	= 4028
MFILLEN	= MYPDLEN-$180

MYPDADR	= $6000

	*=$5000

DBUF	= $304
DSEC	= $30A
SIOVEC	= $E459

	.MACRO WSEC128 ;DRIVENO,SECTOR,ADR
	LDA #$31
	STA $300
	LDA %1
	STA $301
	LDA #$50
	STA $302
	LDA #$80
	STA $303
	LDA #<(%3)
	STA $304
	LDA #>(%3)
	STA $305
	LDA #7
	STA $307
	LDA #128
	STA $308
	LDA #0
	STA $309
	LDA #<(%2)
	STA $30A
	LDA #>(%2)
	STA $30B
	JSR $E459
	.ENDM

; IOCB-Konstante

CIOV	= $E456

ICCOM	= $342
ICSTA	= $343
ICBAL	= $344
ICBAH	= $345
ICBLL	= $348
ICBLH	= $349
ICAX1	= $34A
ICAX2	= $34B

; CIO-Befehle

COPEN	=   3
CCLSE	=  12
CGTXT	=   5
CPTXT	=   9
CGBIN	=   7
CPBIN	=  11

EOL	= $9B

	.MACRO KANNUM ;KANAL
	LDA #%1		;IOCB-Offset
	ASL		;aus Kanalnr.
	ASL		;(mal 16)
	ASL
	ASL
	TAX	;ERGEBNIS IM X-REG
	.ENDM

	.MACRO OPEN ; KANAL,AUX1,AUX2,FILENAME
	KANNUM %1
	LDA #%2
	STA ICAX1,X
	LDA #%3
	STA ICAX2,X
	LDA #COPEN
	STA ICCOM,X
	LDA #<(%4)
	STA ICBAL,X
	LDA #>(%4)
	STA ICBAH,X
	JSR CIOV
	.ENDM

	.MACRO CLOSE ; KANAL
	KANNUM %1
	LDA #CCLSE
	STA ICCOM,X
	JSR CIOV
	.ENDM

	.MACRO NOTE ; KANAL,SECTOR
	KANNUM %1
	LDA #$26
	STA ICCOM,X
	JSR CIOV
	LDA $34C,X
	STA %2
	LDA $34D,X
	STA %2+1
	.ENDM

	.MACRO PRINT ; KANAL,LABEL
	KANNUM %1
	LDA #CPTXT
	STA ICCOM,X
	LDA #<(%2)
	STA ICBAL,X
	LDA #>(%2)
	STA ICBAH,X
	LDA #127		; max. Laenge
	STA ICBLL,X
	LDA #0
	STA ICBLH,X
	JSR CIOV
	.ENDM

	.MACRO BPUT ; KANAL,LAENGE,BUFFER
	KANNUM %1
	LDA #CPBIN
	STA ICCOM,X
	LDA #<(%2)
	STA ICBLL,X
	LDA #>(%2)
	STA ICBLH,X
	LDA #<(%3)
	STA ICBAL,X
	LDA #>(%3)
	STA ICBAH,X
	JSR CIOV
	.ENDM



;************************************

	LDA 82
	STA MERK82
	LDA #0
	STA 82
MAINLOOP	BPUT 0,TITLEN,TITEL
ASKDRV	JSR GETKEY
	CMP #27
	BNE NABORT1
	JMP ABORT
NABORT1	CMP #$31
	BCC INVDSK
	CMP #$39
	BCS INVDSK

	STA INSERT1
	STA PDRIVNO
	SEC
	SBC #$30
	STA DRIVENO
	BPUT 0, INS1LEN, INSERT1
	JMP WTKEY

INVDSK	BPUT 0, 1, BEEP
	JMP ASKDRV

WTKEY	JSR GETKEY
	CMP #27
	BEQ ABORT
	JSR INIT
	PRINT 0,AGAIN

GKLP	JSR GETKEY
	CMP #'Y
	BEQ DOAGAIN
	CMP #'y
	BEQ DOAGAIN
	CMP #'N
	BEQ ABORT
	CMP #'n
	BEQ ABORT
	JMP GKLP

DOAGAIN	PRINT 0,CR
	JMP MAINLOOP

ABORT	LDA MERK82
	STA 82
	RTS

MERK82	.BYTE 0

INIT	CLC
	LDA DRIVENO
	ADC #$30
	STA FILENAME+1
	OPEN 1,8,0,FILENAME
	NOTE 1,MYPDADR+9
	BPUT 1,MFILLEN,(MYPDADR+$180)
	CLOSE 1

	JSR GETDENS
	LDA SECBYTE
	STA MYPDADR+11
	LDA SECMASK
	STA MYPDADR+12
	LDA $308
	STA MYPDADR+13
	LDA $309
	STA MYPDADR+14

	WSEC128 DRIVENO,1,MYPDADR
	WSEC128 DRIVENO,2,MYPDADR+128
	WSEC128 DRIVENO,3,MYPDADR+256
	RTS

GETKEY	LDA $E425
	PHA
	LDA $E424
	PHA
	RTS

	.INCLUDE "getdens.src"

DRIVENO	.BYTE 1
SECBYTE	.BYTE 125
SECMASK	.BYTE 3
SECLEN	.WORD 128

FILENAME	.BYTE "D1:PICODOS.SYS"
		.BYTE EOL

;	       1234567890123456789012345678901234567890
TITEL	.BYTE 125
	.BYTE "          MyPicoDos Init V3.1"
	.BYTE 155
	.BYTE "         (c) HiassofT 1992-2003"
	.BYTE 155,155
	.BYTE "  highspeed version with basic loader"
	.BYTE 155
	.BYTE "  for SD & DD disks, 720-65535 sectors"
	.BYTE 155,155,155
	.BYTE "this program MUST be "
	.BYTE "loaded from DOS,"
	.BYTE 155
	.BYTE "because it creates a "
	.BYTE "short system-file"
	.BYTE 155,155,155
	.BYTE "write MyPicoDos to drive (1-8): "

TITLEN	= *-TITEL

INSERT1	.BYTE "1",155
	.BYTE "insert disk in D"
PDRIVNO	.BYTE "1: and press any key"
	.BYTE 155
INS1LEN	= * -INSERT1

AGAIN	.BYTE "another disk "
	.BYTE "(Y/N)?"
CR	.BYTE 155

BEEP	.BYTE 253

SECBUF	= *

; run address
	* = $2e0
	.WORD $5000

