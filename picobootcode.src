;  PicoBoot - COM loader for a single file, fits into boot sectors
;
;  Copyright (C) 2008-2010 Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

	.include "common.inc"

PICOSTART = $0800


	* = PICOSTART

	.BYTE $80
	.BYTE (PICOEND-PICOSTART+127)/128
	.WORD PICOSTART
	.WORD ENDX

; preconditions: SIO data at $0300 must be setup like this:
; $0300 = $31 (D:)
; $0301 = $01 (drive 1)
; $0302 = $52 (read)
; $0306 = $07 (timeout 7 seconds)
; $0308 = $80 (128 bytes per sector)
; $0309 = $00

; setup sector length and pointers for SD/DD

	LDX #125
	LDA PICOSTART	; bytes per sector
	BNE ?ISSD
	STA DBYT
	INC DBYT+1
	LDX #253
?ISSD	STX SECBYTE

; setup sector buffer
	LDX #0
	STX DBUF
	INX
	STX DSEC+1
	LDA #>BUF
	STA DBUF+1
	
; read first directory sector
	LDA #$69
	STA DSEC

	LDA #$40
	STA $303
	JSR $E459

; use 16 bit sector links if bit 2 of the file status is set
	LDA BUF
	AND #4
	BEQ NO16
	LDA #$FF
	STA SECMASK

NO16	LDY SECBYTE
	LDA BUF+3
	STA BUF+1,Y
	LDA BUF+4
	STA BUF,Y

DOLOAD	LDA #<ENDX
	STA 738
	LDA #>ENDX
	STA 739
	
PICOBOOT = 1
	.include "comloadcode.src"

	.include "rreadcode.src"

	.IF ((*-PICOSTART)&127)<>0
	.DC (128-((*-PICOSTART)&127)) 0
	.ENDIF

PICOEND	= *
