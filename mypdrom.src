;  mypdrom - MyPicoDos/MyIDE flashcart version
;
;  Copyright (C) 2007 Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

	.include "cio.inc"

	.include "common.inc"
	.include "cartsio.inc"

; MyPicoDos start address
MYSTART = $1000

; MyPicoDos setup routine
MYRUNA  = $0F00

; temp vectors for MyPicoDos
SRCADR  = $E0
DSTADR  = $E2

;	* = $8000
;	.BYTE $FF

	* = $A000

	.include "cartsiocode-rom.src"

START	LDX #MYSETL
	LDY #0
MYLP1	LDA MYSETUP,Y	; copy init routine
	STA MYRUNA,Y
	INY
	DEX
	BNE MYLP1
	LDA #<MYPDOS
	STA SRCADR
	LDA #>MYPDOS
	STA SRCADR+1
	LDA #<MYSTART
	STA DSTADR
	LDA #>MYSTART
	STA DSTADR+1
	LDX #MYPDPG
	LDY #0
MYLP2	LDA (SRCADR),Y	; copy MyPicoDos code
	STA (DSTADR),Y
	INY
	BNE MYLP2
	INC SRCADR+1
	INC DSTADR+1
	DEX
	BNE MYLP2
	JMP MYRUNA

; MyPicoDos init routine:
; disable cartridge emu and basic, open screen and
; then run MyPicoDos

MYSETUP	SEI
	LDA #0
	STA $D40E
	CARTOFF
	LDA $D013
	STA $3FA
	LDA #$40
	STA $D40E
	CLI

	LDA $D301
	ORA #2
	STA $D301
	LDA $BFFF
	EOR #$FF
	STA $BFFF
	LDY #1
	LDX #$A0
	CMP $BFFF
	BNE NBASOFF
	LDX #$C0
	LDY #0

NBASOFF	STX 106
	STX 740
	STY 1016

	LDA #$70
	STA DSTADR+1
CLRLP1	LDA #0
	STA DSTADR
	TAY
CLRLP	STA (DSTADR),Y
	DEY
	BNE CLRLP
	INC DSTADR+1
	LDA DSTADR+1
	CMP 106
	BNE CLRLP1

	OPEN 0,12,0,EDITOR-MYSETUP+MYRUNA
	JMP MYSTART
EDITOR	.BYTE "E:", 155
MYSETL	= * - MYSETUP

INIT	RTS

MYPDOS	= *
	.incbin "mypdos-code-cartsio.bin"
MYPDPG	= (*-MYPDOS+255) / 256

XEND	= *
	
	* = $BFFA

	.WORD START
	.BYTE 0
	.BYTE 4		; init & start cart, no disk boot allowed
	.WORD INIT

