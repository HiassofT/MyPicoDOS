;  cartsiocode-ram.src - SIO code for accessing the cart, RAM stub
;
;  Copyright (c) 2010 by Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

DOCARSIO

; wait for VCOUNT to reach $70 so that the screen
; doesn't flicker when accessing the cart
?WTSCR	LDA $D40B
	CMP #$70
	BNE ?WTSCR

.if .def COLORS
	LDA #$0F
	STA $D01A
.endif

	SEI		; disable IRQs
	LDA #0		; disable NMIs
	STA $D40E
;	INC CRITIC	; disable deferred VBI

; set cartridge bank containing ROM code
	CARTBASE

; call SIO setup code (check commands, setup addresses etc.)
	JSR CSIOROM
	BMI ?CSEND	; an error ocurred, exit

; check if we need to copy data
	LDA BUFLEN
	ORA BUFLEN+1
	BEQ ?CSEND	; BUFLEN=0, no copying needed

.if .def COLORS
	LDA #$84
	STA $D01A
.endif

; now copy the data
.if .def FASTCOPY
	CARTBNK CSSRCB	; set source bank number
	LDY #0
?CPLP	LDA (CSSRC),Y
	STA (CSDST),Y
	INY
	CPY BUFLEN
	BNE ?CPLP
.else
	LDY #0
?CPLP	CARTBNK CSSRCB	; set source bank number
	LDA (CSSRC),Y
	CARTOFF		; disable cartridge so we can access $8000-$BFFF
	STA (CSDST),Y
	INY
	CPY BUFLEN
	BNE ?CPLP
.endif

?CSEND	CARTOFF		; disable cartridge
;	DEC CRITIC	; re-enable deferred VBI
	LDA #$40	; re-enable NMIs
	STA $D40E
	CLI		; re-enable IRQs

.if .def COLORS
	LDA #0
	STA $D01A
.endif

	LDY DSTATS
	RTS
