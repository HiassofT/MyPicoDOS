;  cartsiocode-osram.src - SIO replacement code for RAM OS
;
;  Copyright (c) 2010 by Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

	.include "cartsio.inc"

	* = $1000
ENTRY	LDY #0		; check slot number
	BNE ?SLOTOK	; yes, slot is active
STDSIO	JMP $1234	; call original OS SIO code

?SLOTOK	LDA $300
	CMP #$31	; D:?
	BNE STDSIO
	LDA $301
	CMP #1		; D1:?
	BNE STDSIO

; wait for VCOUNT to reach $70 so that the screen
; doesn't flicker when accessing the cart
; PAL works fine up to $76 (SD and DD), NTSC needs $70 and only works in SD
.if .def PAL
	LDA #$74
.else
	LDA #$70
.endif
?WTSCR	CMP $D40B
	BNE ?WTSCR

	SEI
	LDA #0
	STA $D40E
;	LDA #1
;	STA CRITIC		; set critic

.if .def COLORS
	LDA #$0F
	STA $D01A
.endif

; set cartridge bank containing ROM code
	CARTBASE

; call alternate SIO setup code (check commands, setup addresses etc.)
	JSR CSIOROMY

	BMI ?CSEND	; an error ocurred, exit

; check if we need to copy data
	LDA BUFLEN
	ORA BUFLEN+1
	BEQ ?CSEND	; BUFLEN=0, no copying needed

; now copy the data
.if .def COLORS
	LDA #$84
	STA $D01A
.endif

	LDY #0
.if .def FREEZER2005
	CARTBNK CSSRCB	; set source bank number
?CPLP	CARTON
	LDA (CSSRC),Y
.else
?CPLP	CARTBNK CSSRCB	; set source bank number
	LDA (CSSRC),Y
.endif
	CARTOFF		; disable cartridge so we can access $8000-$BFFF
	STA (CSDST),Y
	INY
	CPY BUFLEN
	BNE ?CPLP

?CSEND	CARTOFF		; disable cartridge
;	DEC CRITIC	; re-enable deferred VBI

	CLI
	LDA #$40
	STA $D40E

.if .def COLORS
	LDA #0
	STA $D01A
.endif

	LDY DSTATS
	RTS
