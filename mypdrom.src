;  mypdrom - MyPicoDos/MyIDE flashcart version
;
;  Copyright (C) 2007 Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

	.include "cio.inc"

.if .def MYIDEROM
	CARTOFF = $D530
.else
.if .def MEGAMAXROM
	CARTOFF = $D580
.else
	.error "specify ROM type!"
.endif
.endif

; MyPicoDos start address
MYSTART = $1000

; MyPicoDos setup routine
MYRUNA  = $0F00

; temp vectors for MyPicoDos
SRCADR  = $E0
DSTADR  = $E2

	ORG = $A000

	* = ORG

;	JSR $E483	; HDINIT

RUNMY	LDX #MYSETL
	LDY #0
MYLP1	LDA MYSETUP,Y	; copy init routine
	STA MYRUNA,Y
	INY
	DEX
	BNE MYLP1
	LDA #<MYPDOS
	STA SRCADR
	LDA #>MYPDOS
	STA SRCADR+1
	LDA #<MYSTART
	STA DSTADR
	LDA #>MYSTART
	STA DSTADR+1
	LDX #MYPDPG
	LDY #0
MYLP2	LDA (SRCADR),Y	; copy MyPicoDos code
	STA (DSTADR),Y
	INY
	BNE MYLP2
	INC SRCADR+1
	INC DSTADR+1
	DEX
	BNE MYLP2
	JMP MYRUNA

; MyPicoDos init routine:
; disable cartridge emu and basic, open screen and
; then run MyPicoDos

MYSETUP	SEI
	LDA #0
	STA $D40E
	STA CARTOFF	; MyPicoDos init routine
	LDA $D013
	STA $3FA
	LDA #$40
	STA $D40E
	CLI

	LDA $D301
	ORA #2
	STA $D301
	LDA $BFFF
	EOR #$FF
	STA $BFFF
	CMP $BFFF
	BNE NBASOFF

	LDA #$C0
	STA 106
	STA 740
	LDA #1
	STA 1016
	LDA #$90
	STA DSTADR+1
	LDX #48
	LDA #0
	STA DSTADR
	TAY
CLRLP	STA (DSTADR),Y
	DEY
	BNE CLRLP
	INC DSTADR+1
	DEX
	BNE CLRLP
NBASOFF	CLOSE 0
	OPEN 0,12,0,EDITOR-MYSETUP+MYRUNA
	JMP MYSTART
EDITOR	.BYTE "E:", 155
MYSETL	= * - MYSETUP

XRTS	RTS

MYPDOS	= *
.if .def MYIDEROM
	.incbin "mypdos-code-hioff.bin"
.endif
.if .def MEGAMAXROM
	.incbin "mypdos-code-r.bin"
.endif
MYPDPG	= (*-MYPDOS+255) / 256

XEND	= *
	
	.DC ($BFFA-*) $FF
	.WORD ORG
	.BYTE 0
	.BYTE 0
	.WORD ORG

